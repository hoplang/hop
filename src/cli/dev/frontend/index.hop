import lucide::ArrowRight
import lucide::ArrowUpRight
import lucide::Search
import lucide::Maximize
import lucide::Maximize2
import components::logo::Logo

record Entrypoint {
  name: String,
  module: String,
}

<EntrypointItem {
  name: String,
  module: String,
}>
  <a
    href={"/preview/" + name}
    class="group flex flex-col gap-4 outline-none"
  >
    <div class={
      join!(
        "w-full",
        "h-40",
        "border",
        "border-neutral-300",
        "rounded",
        "relative",
        "overflow-hidden",
        "group-focus:ring-3",
        "ring-neutral-200",
      )
    }>
      <iframe
        class={
          join!(
            "w-[400%]",
            "h-[400%]",
            "scale-[0.25]",
            "origin-top-left",
            "pointer-events-none",
            "grayscale",
            "group-hover:grayscale-0",
            "group-focus:grayscale-0",
            "transition-[filter]",
          )
        }
        scrolling="no"
        tabindex="-1"
        sandbox=""
        src={"/api/render/" + name}
      >
      </iframe>
      <div class={
        join!(
          "absolute",
          "right-0",
          "bottom-0",
          "z-index-1000",
          "opacity-0",
          "p-2",
          "bg-white",
          "rounded-tl",
          "group-hover:opacity-100",
          "transition-opacity",
        )
      }>
        <Maximize/>
      </div>
    </div>
    <div class="flex flex-col gap-0.5">
      <h2 class="text-lg text-[#111]">
        {name}
      </h2>
      <p class="text-sm text-[#666666]">
        in {module}
      </p>
    </div>
  </a>
</EntrypointItem>

entrypoint Index(
  entrypoints: Array[Entrypoint] = [
    Entrypoint {module: "index.hop", name: "Index"},
    Entrypoint {module: "overlay.hop", name: "Overlay"},
  ],
) {
  <head>
    <link
      rel="stylesheet"
      href="https://rsms.me/inter/inter.css"
    >
  </head>
  <body
    class="bg-[#f3f3f3]"
    style="font-family: Inter, sans-serif;"
  >
    <div class="max-w-6xl px-6 mx-auto my-8 flex flex-col">
      <a
        href="/"
        class={
          join!(
            "mt-1",
            "lg:mt-2",
            "mb-16",
            "xl:mb-32",
          )
        }
        tabindex="-1"
      >
        <Logo/>
      </a>
      <div class="flex flex-col gap-6 xl:gap-8">
        <h1 class="text-3xl md:text-4xl font-light">
          Entrypoints
        </h1>
        <match {entrypoints.is_empty()}>
          <case {true}>
            <p class="text-[#666666]">
              No entrypoints found.
            </p>
          </case>
          <case {false}>
            <div class={
              join!(
                "grid",
                "sm:grid-cols-2",
                "md:grid-cols-3",
                "lg:grid-cols-4",
                "gap-12",
                "sm:gap-8",
                "lg:gap-14",
              )
            }>
              <for {ep in entrypoints}>
                <EntrypointItem
                  name={ep.name}
                  module={ep.module}
                />
              </for>
            </div>
          </case>
        </match>
      </div>
    </div>
  </body>
}
